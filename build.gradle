plugins {
	id 'java'
    id 'pmd'
    id 'com.diffplug.spotless' version '6.25.0'
	id 'org.springframework.boot' version '3.0.5'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'org.graalvm.buildtools.native' version '0.9.20'
}

spotless {
    java {
        target 'src/main/**/*.java', 'src/test/**/*.java'

        googleJavaFormat()
        trimTrailingWhitespace()
        removeUnusedImports()
        endWithNewline()
    }
}

pmd {
    toolVersion = '6.55.0'
    ruleSets = []
	ruleSetFiles = files(".pmd.xml")
}

tasks.withType(Pmd) {
    source = fileTree('src') {
        include '**/*.java'
        exclude '**/test/**'
    }
}

bootJar {
    archiveBaseName.set("psoft-commerce")
    archiveVersion.set("")
    archiveClassifier.set("")
}

group = 'com.ufcg.psoft'
version = '1.0.0'
sourceCompatibility = '17'

compileJava.options.encoding = 'UTF-8'
bootRun.systemProperties['file.encoding'] = 'UTF-8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.postgresql:postgresql:42.6.0'

	implementation 'org.flywaydb:flyway-core'
	compileOnly 'org.projectlombok:lombok:1.18.30'
	implementation 'org.modelmapper:modelmapper:3.1.1'

    implementation 'org.apache.commons:commons-csv:1.9.0'
	/*Object Mapper suporte para LocalDateTime*/
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.2'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.1.0'

	pmd 'net.sourceforge.pmd:pmd-java:6.55.0'
}

tasks.named('test') {
	useJUnitPlatform()
}
